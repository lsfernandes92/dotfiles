# shortcut to this dotfiles path;
# oh-my-zsh expect that;
# DON'T "export DOT_FILES=$HOME/.dotfiles"
export ZSH=$HOME/.oh-my-zsh

# set name of the theme to load;
# (optional) set this to "random"
#
# see https://github.com/robbyrussell/oh-my-zsh/wiki/Themes
ZSH_THEME="agnoster"

# which oh-my-zsh plugins 
# would you like to load?
#
# add wisely, as too many plugins
# slow down shell startup.
# plugins=(git)

# set env lang
export LANG="en_US.UTF-8"
export LC_ALL="en_US.UTF-8"

source $ZSH/oh-my-zsh.sh
# remove oh-my-zsh default aliases
unalias l 2>/dev/null

# stash your environment variables in ~/.localrc;
# this means they'll stay out
# of your main dotfiles repository 
# (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($HOME/.dotfiles/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  # block prompt overrides
  if ! grep -qE 'PROMPT=|ZSH_THEME=|promptinit|starship|p10k' "$file"; then
    source "$file"
  fi
done

# enable docker cli completitions;
# by docker desktop
fpath=(/Users/ritalee/.docker/completions $fpath)

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# better history
#
# see https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down
bindkey -e
bindkey '\e\e[C' forward-word
bindkey '\e\e[D' backward-word
